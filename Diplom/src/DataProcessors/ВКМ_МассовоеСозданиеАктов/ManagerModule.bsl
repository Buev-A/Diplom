#Область СлужебныеПроцедурыИФункции

Функция СоздатьСписокНаСервере (ДатаНачала, ДатаОкончания) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РеализацияТоваровУслуг.Ссылка КАК Ссылка
		|ПОМЕСТИТЬ ВТ_Реализации
		|ИЗ
		|	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
		|ГДЕ
		|	РеализацияТоваровУслуг.Дата МЕЖДУ &Дата1 И &Дата2
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДоговорыКонтрагентов.Ссылка КАК Договор,
		|	ДоговорыКонтрагентов.Владелец КАК Владелец,
		|	ДоговорыКонтрагентов.Организация КАК Организация,
		|	ВТ_Реализации.Ссылка КАК Реализация,
		|	&Дата2 КАК ДатаОкончания
		|ИЗ
		|	ВТ_Реализации КАК ВТ_Реализации
		|		ПРАВОЕ СОЕДИНЕНИЕ Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
		|		ПО (ДоговорыКонтрагентов.Ссылка = ВТ_Реализации.Ссылка.Договор)
		|ГДЕ
		|	ДоговорыКонтрагентов.ВидДоговора = ЗНАЧЕНИЕ(Перечисление.ВидыДоговоровКонтрагентов.ВКМ_АбонентскоеОбслуживание)
		|	И ДоговорыКонтрагентов.ВКМ_НачалоДействияДоговора <= &Дата1
		|	И ДоговорыКонтрагентов.ВКМ_КонецДействияДоговора >= &Дата2";
	
	Запрос.УстановитьПараметр("Дата1", 	ДатаНачала);
	Запрос.УстановитьПараметр("Дата2", 	ДатаОкончания);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();

	СформированныйСписокРеализации = СформироватьСписокРеализации(Выборка);

	Возврат СформированныйСписокРеализации;   
	
КонецФункции  

Функция СформироватьСписокРеализации(Выборка)

	СписокРеализацийМассив = Новый Массив;
	
	Пока Выборка.Следующий() Цикл 
		
		СписокРеализацийСтруктура = Новый Структура;
		
		Если Не ЗначениеЗаполнено(Выборка.Реализация) Тогда
			НоваяРеализация = Документы.РеализацияТоваровУслуг.СоздатьДокумент();
			НоваяРеализация.Дата = Выборка.ДатаОкончания;
			НоваяРеализация.Ответственный = Пользователи.ТекущийПользователь();	
			НоваяРеализация.Договор = Выборка.Договор; 
			НоваяРеализация.Контрагент = Выборка.Владелец;
			НоваяРеализация.Организация = Выборка.Организация;
			НоваяРеализация.ВКМ_ВыполнитьАвтозаполнение(Выборка.Договор);
			Если НоваяРеализация.ПроверитьЗаполнение() Тогда        
				НоваяРеализация.Записать(РежимЗаписиДокумента.Проведение, РежимПроведенияДокумента.Неоперативный);
			КонецЕсли;
			СписокРеализацийСтруктура.Вставить("Договор", Выборка.Договор);
			СписокРеализацийСтруктура.Вставить("Реализация", НоваяРеализация.Ссылка);
		Иначе                                                                 
			ДокРеализация = Выборка.Реализация.ПолучитьОбъект();
			ДокРеализация.ВКМ_ВыполнитьАвтозаполнение(Выборка.Договор);
			Если ДокРеализация.ПроверитьЗаполнение() Тогда        
				ДокРеализация.Записать(РежимЗаписиДокумента.Проведение, РежимПроведенияДокумента.Неоперативный);
			КонецЕсли;

			СписокРеализацийСтруктура.Вставить("Договор", Выборка.Договор);
			СписокРеализацийСтруктура.Вставить("Реализация", Выборка.Реализация);  
		КонецЕсли; 
		
		СписокРеализацийМассив.Добавить(СписокРеализацийСтруктура);
		
	КонецЦикла;      
	
	Возврат СписокРеализацийМассив; 
	
КонецФункции

#КонецОбласти